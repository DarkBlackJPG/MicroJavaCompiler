

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}



/* ==================== TERMINALS ==================== */

terminal String IDENT;
terminal Integer NUM_CONST;
terminal Character CHAR_CONST;
terminal Integer BOOL_CONST;

terminal COMMA;

terminal PROG;

terminal MULTIPLY;
terminal DIVIDE;
terminal MODULO;

terminal PLUS;
terminal MINUS;

terminal EQUALS;
terminal NOT_EQUAL;
terminal LARGER_THAN;
terminal LARGER_EQUAL;
terminal LESS_THAN;
terminal LESS_EQUAL;

terminal ASSIGN;

terminal BRACES_BEGIN;
terminal BRACES_END;

terminal BRACKET_BEGIN;
terminal BRACKET_END;

terminal PARENTHESES_BEGIN;
terminal PARENTHESES_END;

terminal CONST;

terminal SEMI;

terminal CLASS;
terminal EXTENDS;
terminal VOID;

terminal IF;
terminal ELSE;
terminal BREAK;
terminal CONTINUE;
terminal RETURN;
terminal OR;
terminal NEW;
terminal AND;
terminal DOT;
terminal INCREMENT;
terminal DECREMENT;
terminal PRINT;
terminal DO;
terminal WHILE;
terminal SWITCH;
terminal CASE;
terminal COLON;
terminal READ;
terminal QUESTION_MARK;


/* ================== NON-TERMINALS ================== */

nonterminal Program;

nonterminal MulOp;

nonterminal RelOp;

nonterminal AssignOp;

nonterminal AddOp;

nonterminal Declarations;

nonterminal MethodDeclarations;

nonterminal Decls;

nonterminal Type;

nonterminal ConstList;

nonterminal Const;

nonterminal Literals;

nonterminal VariableDeclarations;

nonterminal VariableList;

nonterminal VariableDeclaration;

nonterminal ClassDeclaration;

nonterminal ClassBody;

nonterminal FieldList;

nonterminal ClassMethodList;

nonterminal ReturnType;

nonterminal MethodDeclaration;

nonterminal MethodVariables;

nonterminal FormPars;

nonterminal Statement;

nonterminal DesignatorStatement;

nonterminal Condition;

nonterminal ReturnStmts;

nonterminal Expr;

nonterminal StatementList;

nonterminal CondExpression;

nonterminal CondTerm;

nonterminal FormalParameters;

nonterminal CondFactor;

nonterminal Term;

nonterminal Factor;

nonterminal Designator;

nonterminal DesignatorList;

nonterminal ErrorDecls;

nonterminal SwichCaseDefinition;

nonterminal ActPars;

nonterminal PrintStmtList;

nonterminal SwitchCaseList;

nonterminal CaseStatementList;

nonterminal RelationalExpression;

nonterminal ExprTern;

nonterminal ExprNoTern;

nonterminal FormParamsBrackets;

nonterminal ExprNoTernAddOpList;

nonterminal MethodStatements;
nonterminal ActParsChoice;
nonterminal ErrorConstList;

/*
nonterminal TernaryCondition;

nonterminal TernaryCondExpression;

nonterminal TernaryCondTerm ;
			
nonterminal TernaryCondFactor;

nonterminal TernaryRelationalExpression ;
*/


/* ===================== GRAMMAR ===================== */

precedence left ELSE;
start with Program;
Program 	::= 
			  (Program) 			PROG 
			  						IDENT:programName
			  						Declarations
			  						BRACES_BEGIN
			  						MethodDeclarations
			  						BRACES_END
			  	;

Declarations ::=
				(DeclarationList) Declarations Decls
				| (NoDecls)
				;
MethodDeclarations ::= (SingleMethod) MethodDeclaration
					| (MethodDeclList) MethodDeclarations MethodDeclaration
					;
Decls		::= 
				(ErrorDeclarations) ErrorDecls
				| (ClassDeclarations) CLASS ClassDeclaration BRACES_BEGIN ClassBody BRACES_END
				;

ErrorDecls ::= (ConstDeclarations) CONST Type ConstList SEMI
				| (VarDeclarations) VariableDeclarations SEMI
				|
					error SEMI:l
					{:
						parser.report_error("Izvrsen oporavak do ; u linijij" + lleft, null);
					:}
				;

ConstList 	::= 
				(SingleConst) Const
				| (ErrorConstListDecl) ErrorConstList;
				
ErrorConstList ::= (MoreConsts) ConstList COMMA Const
					|
					error COMMA:l
					{:
						parser.report_error("Izvrsen oporavak do ; u linijij" + lleft, null);
					:}
					;
				
Const		::=
				(ConstDefinition) IDENT:identification AssignOp Literals;

Literals	::=
				(NumericLiteral) NUM_CONST:numValue
				| (CharLiteral) CHAR_CONST:charValue
				| (BooleanLiteral) BOOL_CONST:booleanValue
				;


VariableDeclarations ::= 
					(VariableDecls) Type VariableList 
					
					;		
	
VariableList ::= 
				(SingleVariable) VariableDeclaration
				| (MoreVariables) VariableList COMMA VariableDeclaration
				| error COMMA:l
				{:
					parser.report_error("Izvrsen oporavak do , u linijij" + lleft, null);
				:}
				;
				
VariableDeclaration ::= 
					(NonArrayVariable) IDENT:variableName
					| (ArrayVariable) IDENT:variableName BRACKET_BEGIN BRACKET_END
					;

ClassDeclaration ::= 
				(RootClass) IDENT:className
				| (ChildClass) IDENT:className EXTENDS Type
				;


ClassBody ::= 
			(ClassBodyWithMethods) FieldList BRACES_BEGIN ClassMethodList BRACES_END
			| (ClassBodyWithoutMethods) FieldList
			;
			
FieldList ::=
			(HasFieldVariables) FieldList VariableDeclarations
			| (NoFields)
			;
			

ClassMethodList ::=
				(HasMethods) ClassMethodList MethodDeclaration
				|
				(NoMethods)
				; 

MethodDeclaration ::= (OneMethodDeclaration) ReturnType IDENT:methodIdentification PARENTHESES_BEGIN FormalParameters PARENTHESES_END MethodVariables BRACES_BEGIN MethodStatements BRACES_END
				;

MethodStatements ::= (NonEmptyMethodStatements) MethodStatements Statement
				| (EmptyMethodStatements)
				;

FormalParameters ::= (NonEmptyFormalPars) FormPars
					| (EmptyFormPars)
					;

FormPars ::= (FormParsDefinition) Type IDENT:formParamName FormParamsBrackets
			|
			(FormParsDefinitionList) FormPars COMMA Type IDENT:paramName FormParamsBrackets
			;
FormParamsBrackets ::= (HasBrackets) BRACKET_BEGIN BRACKET_END
					| (DoesntHaveBrackets)
					;
ReturnType ::= (TypedReturn) Type
			| (VoidReturn) VOID
			;

MethodVariables ::= 
				(HasMethodVariables) MethodVariables VariableDeclarations
				|
				(NoVariables)
				;


Statement 		::= (StatementDesignatorStatement) DesignatorStatement SEMI
				| (IfThenStatement) IF PARENTHESES_BEGIN Condition PARENTHESES_END Statement
				| (IfThenElseStatement) IF PARENTHESES_BEGIN Condition PARENTHESES_END Statement ELSE Statement
				| (DoWhileStatement) DO Statement WHILE PARENTHESES_BEGIN Condition PARENTHESES_END SEMI
				| (SwitchStatement) SWITCH PARENTHESES_BEGIN Expr PARENTHESES_END BRACES_BEGIN SwitchCaseList BRACES_END
				| (BreakStmt) BREAK SEMI
				| (ContinueStmt) CONTINUE SEMI
				| (ReturnStmt) RETURN ReturnStmts SEMI
				| (ReadStmt) READ PARENTHESES_BEGIN Designator PARENTHESES_END SEMI
				| (PrintStmt) PRINT PARENTHESES_BEGIN PrintStmtList PARENTHESES_END SEMI
				| (StatementsList) BRACES_BEGIN StatementList BRACES_END
				;
				
SwitchCaseList ::= 
				(EmptySwitchCaseList) /* epsilon */
				| (NonEmptySwitchCaseList) SwitchCaseList SwichCaseDefinition
				;
				
SwichCaseDefinition ::= (SingleSwitchCaseDefinition) CASE NUM_CONST:numberValue COLON CaseStatementList ;

CaseStatementList ::= (NonEmptyCaseStatementList) CaseStatementList Statement |
					(EmptyCaseStatementList)
					;

PrintStmtList ::= (NoNumConst) Expr
				| (WithNumConst) Expr COMMA NUM_CONST:intValue;
				
StatementList ::= (NonEmptyStatementList) StatementList Statement
					| (EmptyStatement) ;
					
					
DesignatorStatement ::= (AssignDesignator) Designator AssignOp Expr
					| (MethodCallDesignator) Designator PARENTHESES_BEGIN ActPars PARENTHESES_END
					| (IncrementDesignator) Designator INCREMENT
					| (DecrementDesignator) Designator DECREMENT
					;


ReturnStmts ::= (NonEmptyReturn) Expr 
			| (EmptyReturn)
			;

Condition ::= (Condition) CondExpression ;


CondExpression ::= 
				(ConditionExpressionWithoutOr) CondTerm 
				| (ConditionExpressionWithOr) CondExpression OR CondTerm
				;

CondTerm ::= (ConditionTermWithAnd) CondTerm AND CondFactor
			| (ConditionTermFactor) CondFactor
			;
			
CondFactor ::= (ExpressionConditionFactor) ExprNoTern RelationalExpression
			;

RelationalExpression ::= (NonEmptyRelationalExpression) RelOp ExprNoTern
					| (EmptyRelationalExpression)
					;

/*
TernaryCondition ::= (TernaryCondition) TernaryCondExpression ;


TernaryCondExpression ::= 
				(TernaryConditionExpressionWithoutOr) TernaryCondTerm 
				| (TernaryConditionExpressionWithOr) TernaryCondExpression OR TernaryCondTerm
				;

TernaryCondTerm ::= (TernaryConditionTermWithAnd) TernaryCondTerm AND TernaryCondFactor
			| (TernaryConditionTermFactor) TernaryCondFactor
			;
			
TernaryCondFactor ::= (TernaryExpressionConditionFactor) ExprNoTern
			;

TernaryRelationalExpression ::= (TernaryNonEmptyRelationalExpression) RelOp ExprNoTern
					| (TernaryEmptyRelationalExpression)
					;
*/
Expr ::=  (ExprNoTernStatement) ExprNoTern
		|(ExprTernStatement) ExprTern
	;
	
ExprNoTern ::=  (NegativeTerm) MINUS ExprNoTernAddOpList
	| (AddOperationTerm) ExprNoTernAddOpList
	;
	
ExprNoTernAddOpList ::= (TermList) ExprNoTernAddOpList AddOp Term
						| (OneTerm) Term
						;
	
ExprTern ::= (TernaryExpressionStmt) Condition QUESTION_MARK Expr COLON Expr
			;


Term ::= (NonMullOpTerm) Factor
		| (MullOpTerm) Term MulOp Factor
		;
	
Factor ::=
		(NoParamsDesignator) Designator 
		| (ParamsDesignator) Designator PARENTHESES_BEGIN ActParsChoice PARENTHESES_END
		| (NumericConstant) NUM_CONST:numValue
		| (CharacterConstant) CHAR_CONST:charValue
		| (BooleanConstant) BOOL_CONST:boolValue
		| (NewType) NEW Type
		| (NewTypeArray) NEW Type BRACKET_BEGIN Expr BRACKET_END
		| (ParenthesesExpression) PARENTHESES_BEGIN Expr PARENTHESES_END
		;

ActParsChoice ::= (EmptyActPars)
		| (NonEmptyActPars) ActPars
		;

ActPars ::= (ActParsWithoutComma) Expr
		| (ActParsWithComma) ActPars COMMA Expr;
		
		
Designator ::= (CleanDesignator) IDENT:identification
			| (FieldCallDesignator) DesignatorList DOT IDENT:identification
			| (ArrayElementDesignator) DesignatorList BRACKET_BEGIN Expr BRACKET_END
			;

DesignatorList ::= (CleanDesignatorList) IDENT:identification
				| (FieldCallDesignatorList) DesignatorList DOT IDENT:identification
				| (ArrayElementDesignatorList) DesignatorList BRACKET_BEGIN Expr BRACKET_END
				;

Type 			::= 
		(Type) IDENT:type
		;

RelOp 		::= 
			  (RelopEquals) 		EQUALS
			| (RelopNotEqual)		NOT_EQUAL
			| (RelopLargerThan) 	LARGER_THAN
			| (RelopLargerEqual) 	LARGER_EQUAL
			| (RelopLessThan)		LESS_THAN
			| (RelopLessEqual) 		LESS_EQUAL
			;
		
AssignOp 	::= 
			   (AssignOp) 			ASSIGN
			;

AddOp		 ::= 
			  (AddopPlus) 			PLUS
			| (AddopMinus) 			MINUS
			;

MulOp 		::= 
			  (MulopTimes) 			MULTIPLY
			| (MulopDivide)			DIVIDE
			| (MulopModulo)			MODULO
			;
