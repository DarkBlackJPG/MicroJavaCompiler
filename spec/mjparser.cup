

package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }


:}


scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}



/* ==================== TERMINALS ==================== */

terminal String IDENT;
terminal Integer NUM_CONST;
terminal Character CHAR_CONST;
terminal Integer BOOL_CONST;

terminal COMMA;

terminal PROG;

terminal TIMES;
terminal DIVIDE;
terminal MODULO;

terminal PLUS;
terminal MINUS;

terminal EQUALS;
terminal NOT_EQUAL;
terminal LARGER_THAN;
terminal LARGER_EQUAL;
terminal LESS_THAN;
terminal LESS_EQUAL;

terminal ASSIGN;

terminal BRACES_BEGIN;
terminal BRACES_END;

terminal BRACKET_BEGIN;
terminal BRACKET_END;

terminal PARENTHESES_BEGIN;
terminal PARENTHESES_END;

terminal CONST;

terminal SEMI;

terminal CLASS;
terminal EXTENDS;
terminal VOID;

terminal IF;
terminal BREAK;
terminal CONTINUE;
terminal RETURN;
terminal OR;
terminal NEW;
terminal AND;
terminal DOT;
terminal INCREMENT;
terminal DECREMENT;
terminal PRINT;


/* ================== NON-TERMINALS ================== */

nonterminal Program;

nonterminal MulOp;

nonterminal RelOp;

nonterminal AssignOp;

nonterminal AddOp;

nonterminal Declarations;

nonterminal MethodDeclarations;

nonterminal Decls;

nonterminal Type;

nonterminal ConstList;

nonterminal Const;

nonterminal Literals;

nonterminal VariableDeclarations;

nonterminal VariableList;

nonterminal VariableDeclaration;

nonterminal ClassDeclaration;

nonterminal ClassBody;

nonterminal FieldList;

nonterminal ClassMethodList;

nonterminal ReturnType;

nonterminal MethodDeclaration;

nonterminal MethodVariables;

nonterminal FormPars;

nonterminal Statement;

nonterminal DesignatorStatement;

nonterminal Condition;

nonterminal ReturnStmts;

nonterminal Expr;

nonterminal StatementList;

nonterminal CondExpression;

nonterminal CondTerm;

nonterminal CondFactor;

nonterminal Term;

nonterminal Factor;

nonterminal Designator;

nonterminal DesignatorList;

nonterminal ActPars;

nonterminal PrintStmtList;
/* ===================== GRAMMAR ===================== */

Program 	::= 
			  (Program) 			PROG 
			  						IDENT:programName
			  						Declarations
			  						BRACES_BEGIN
			  						MethodDeclarations
			  						BRACES_END;

Declarations ::=
				(DeclarationList) Declarations Decls
				| (NoDecls)
				;
				
Decls		::= 
				(ConstDeclarations) CONST Type ConstList SEMI
				| (VarDeclarations) VariableDeclarations
				| (ClassDeclarations) CLASS ClassDeclaration BRACES_BEGIN ClassBody BRACES_END
				;

ConstList 	::= 
				(SingleConst) Const
				| (MoreConsts) ConstList COMMA Const;
				
Const		::=
				(ConstDefinition) IDENT:identification ASSIGN Literals;

/* BoolLiteral ima neki random bag da generise neki kod koji ne moze da koristi */

Literals	::=
				(NumericLiteral) NUM_CONST:numValue
				| (CharLiteral) CHAR_CONST:charValue
				| (BooleanLiteral) BOOL_CONST:booleanValue
				;


VariableDeclarations ::= 
					(VariableDecls) Type VariableList SEMI
					;		
	
VariableList ::= 
				(SingleVariable) VariableDeclaration
				| (MoreVariables) VariableList COMMA VariableDeclaration
				;
				
VariableDeclaration ::= 
					(NonArrayVariable) IDENT:variableName
					| (ArrayVariable) IDENT:variableName BRACKET_BEGIN BRACKET_END
					;

ClassDeclaration ::= 
				(RootClass) IDENT:className
				| (ChildClass) IDENT:className EXTENDS Type
				;


ClassBody ::= 
			(ClassBodyWithMethods) FieldList BRACES_BEGIN ClassMethodList BRACES_END
			| (ClassBodyWithoutMethods) FieldList
			;
			
FieldList ::=
			(HasFieldVariables) FieldList VariableDeclarations
			| (NoFields)
			;
			

ClassMethodList ::=
				(HasMethods) ClassMethodList MethodDeclaration
				|
				(NoMethods)
				; 

MethodDeclaration ::= 
				(OneMethodDeclaration) ReturnType
				IDENT 
				PARENTHESES_BEGIN 
					FormPars 
				PARENTHESES_END
				
				MethodVariables 
				
				BRACES_BEGIN
					Statement
				BRACES_END
				;

MethodVariables ::= 
				(HasMethodVariables) MethodVariables VariableDeclarations
				|
				(NoVariables)
				;


Statement 		::= (DesignatorStatement) DesignatorStatement SEMI
				| (BreakStmt) BREAK SEMI
				| (ContinueStmt) CONTINUE SEMI
				| (ReturnStmt) RETURN ReturnStmts SEMI
				| (PrintStmt) PRINT PARENTHESES_BEGIN PrintStmtList PARENTHESES_END SEMI
				| (StatementsList) BRACES_BEGIN StatementList BRACES_END
				;
PrintStmtList ::= (NoNumConst) Expr
				| (WithNumConst) Expr COMMA NUM_CONST;
				
StatementList ::= (NonEmptyStatementList) StatementList Statement
					| (EmptyStatement) ;
					
					
DesignatorStatement ::= (AssignDesignator) Designator ASSIGN Expr
					| (MethodCallDesignator) Designator PARENTHESES_BEGIN ActPars PARENTHESES_END
					| (IncrementDesignator) Designator INCREMENT
					| (DecrementDesignator) Designator DECREMENT
					;

Condition ::= (EMPTY);

ReturnStmts ::= (NonEmptyReturn) Expr 
			| (EmptyReturn);

CondExpression ::= 
				(ConditionExpressionWithoutOr) CondTerm 
				| (ConditionExpressionWithOr) CondExpression OR CondTerm
				;

CondTerm ::= (ConditionTermWithAnd) CondExpression AND CondFactor
			| (ConditionTermFactor) CondFactor
			;
			
CondFactor ::= (ExpressionConditionFactor) Expr
			| (RelOpConditionFactor) Expr RelOp Expr
			;
			
			
Expr ::= (Term) Term
	| (NegativeTerm) MINUS Term
	| (AddOperationTerm) Expr AddOp Term;

Term ::= (NonMullOpTerm) Factor
		| (MullOpTerm) Term MulOp Factor
		;
	
Factor ::=
		(NoParamsDesignator) Designator 
		| (ParamsDesignator) Designator PARENTHESES_BEGIN ActPars PARENTHESES_END
		| (NumericConstant) NUM_CONST:numValue
		| (CharacterConstant) CHAR_CONST:charValue
		| (BooleanConstant) BOOL_CONST:boolValue
		| (NewType) NEW Type
		| (NewTypeArray) NEW Type BRACKET_BEGIN Expr BRACKET_END
		| (ParenthesesExpression) PARENTHESES_BEGIN Expr PARENTHESES_END
		;

ActPars ::= (ActParsWithoutComma) Expr
		| (ActParsWithComma) ActPars COMMA Expr;
Designator ::= (CleanDesignator) IDENT:identification
			| (FieldCallDesignator) DesignatorList DOT IDENT:identification
			| (ArrayElementDesignator) DesignatorList BRACKET_BEGIN Expr BRACKET_END
			;

DesignatorList ::= (CleanDesignatorList) IDENT:identification
				| (FieldCallDesignatorList) DesignatorList DOT IDENT:identification
				| (ArrayElementDesignatorList) DesignatorList BRACKET_BEGIN Expr BRACKET_END
				;

Type 			::= 
		(Type) IDENT:type
		;

RelOp 		::= 
			  (RelopEquals) 		EQUALS
			| (RelopNotEqual)		NOT_EQUAL
			| (RelopLargerThan) 	LARGER_THAN
			| (RelopLargerEqual) 	LARGER_EQUAL
			| (RelopLessThan)		LESS_THAN
			| (RelopLessEqual) 		LESS_EQUAL
			;
		
AssignOp 	::= 
			   (AssignOp) 			ASSIGN
			;

AddOp		 ::= 
			  (AddopPlus) 			PLUS
			| (AddopMinus) 			MINUS
			;

MulOp 		::= 
			  (MulopTimes) 			TIMES
			| (MulopDivide)			DIVIDE
			| (MulopModulo)			MODULO
			;
